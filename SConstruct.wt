# -*- mode: python; -*-
import re

# XXX
# Install
# Java
# Python
# --with-compressors
# make check
# doxygen/docs

EnsureSConsVersion(2, 0, 0)

env = Environment()
conf = env.Configure(config_h='wiredtiger_config.h')

wt_options()

additional_libs = []
if conf.CheckLib('dl'):
    additional_libs.append('dl')
if conf.CheckLib('pthread'):
    additional_libs.append('pthread')

wt_config(conf)
wt_compiler(conf)

env = conf.Finish()

# Build the wiredtiger.h file.
replacements = {
    '@VERSION_MAJOR@' : VERSION_MAJOR,
    '@VERSION_MINOR@' : VERSION_MINOR,
    '@VERSION_PATCH@' : VERSION_PATCH,
    '@VERSION_STRING@' : VERSION_STRING,
    '@uintmax_t_decl@': "",             # XXX: Autoconf compatibility.
    '@uintptr_t_decl@': "",             # XXX: Autoconf compatibility.
}
if os_windows:                          # XXX: Autoconf compatibility.
    replacements.update({'@off_t_decl@' : 'typedef int64_t wt_off_t;'})
else:
    replacements.update({'@off_t_decl@' : 'typedef off_t wt_off_t;'})

env.Substfile(
    target='wiredtiger.h',
    source=[
        'src/include/wiredtiger.in',
    ],
    SUBST_DICT=replacements)

# WiredTiger library sources derived from dist/filelist. Any file list conditionals must appear
# here, and those files will be included if the value is true.
condition_map = {
    'ARM64_HOST' : os_arm64,
    'POSIX_HOST' : os_posix,
    'POWERPC_HOST' : os_powerpc,
    'WINDOWS_HOST' : os_windows,
    'X86_HOST' : os_x86,
    'ZSERIES_HOST' : os_zseries,
}
def filtered_filelist(f):
    for line in f:
        file_cond = line.split()
        if line.startswith("#") or len(file_cond) == 0:
            continue
        if len(file_cond) == 1 or condition_map[file_cond[1]]:
            yield file_cond[0]
filelist = 'dist/filelist'
wtlib_src = list(filtered_filelist(open(filelist)))

if GetOption("enable_static"):
    wtlib = env.StaticLibrary(target="wiredtiger", source=wtlib_src)
else:
    wtlib = env.SharedLibrary(target="wiredtiger", source=wtlib_src)
Default(wtlib)

# Build a utility environment for building test programs.
env_util = env.Clone()
env_util.Append(CPPPATH = ['#test/utility'])
env_util.Append(LIBPATH = ['#', '#test/utility'])
env_util.Append(LIBS = ['wiredtiger', 'test_util'] + additional_libs)

Export('env', 'env_util')
conscript = [
    'examples/c/SConscript',
    'ext/collators/reverse/SConscript',
    'ext/collators/revint/SConscript',
    'ext/compressors/nop/SConscript',
    'ext/encryptors/nop/SConscript',
    'ext/encryptors/rotn/SConscript',
    'ext/extractors/csv/SConscript',
    'ext/test/fail_fs/SConscript',
    'src/utilities/SConscript',
    'test/bloom/SConscript',
    'test/checkpoint/SConscript',
    'test/cursor_order/SConscript',
    'test/fops/SConscript',
    'test/format/SConscript',
    'test/huge/SConscript',
    'test/manydbs/SConscript',
    'test/packing/SConscript',
    'test/readonly/SConscript',
    'test/salvage/SConscript',
    'test/thread/SConscript',
    'test/utility/SConscript']
if GetOption("enable_lz4"):
    conscript.append('ext/compressors/lz4/SConscript')
if GetOption("enable_snappy"):
    conscript.append('ext/compressors/snappy/SConscript')
if GetOption("enable_zlib"):
    conscript.append('ext/compressors/zlib/SConscript')
if GetOption("enable_zstd"):
    conscript.append('ext/compressors/zstd/SConscript')
[SConscript(i) for i in conscript]
